apply plugin: 'com.android.library'
apply from: '../gradle/constants.gradle'
apply from: 'preprocess.gradle'
apply from: 'publish.gradle'

android {
    compileSdkVersion PROJECT_COMPILE_SDK
    buildToolsVersion PROJECT_BUILD_TOOLS

    defaultConfig {
        minSdkVersion PROJECT_MIN_SDK
        targetSdkVersion PROJECT_TARGET_SDK
        versionCode PROJECT_VERSION_CODE
        versionName PROJECT_VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        targetCompatibility PROJECT_TARGET_COMPATIBILITY
        sourceCompatibility PROJECT_SOURCE_COMPATIBILITY
    }

    flavorDimensions "compatibility"
    productFlavors {
        support {
            dimension "compatibility"
            processor.symbols "SUPPORT"
            matchingFallbacks = ['debug', 'release']
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        androidx {
            dimension "compatibility"
            processor.symbols "ANDROIDX"
            matchingFallbacks = ['debug', 'release']
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX flavor
    androidxApi "com.google.android.material:material:$libraryAndroidxMaterialVersion"
    androidxApi "androidx.appcompat:appcompat:$libraryAndroidxVersion"
    androidxApi "androidx.recyclerview:recyclerview:$libraryAndroidxVersion"
    androidxApi "androidx.cardview:cardview:$libraryAndroidxVersion"
    androidxApi "androidx.media:media:$libraryAndroidxVersion"

    // Support flavor
    supportApi "com.android.support:appcompat-v7:$librarySupportVersion"
    supportApi "com.android.support:design:$librarySupportVersion"
    supportApi "com.android.support:support-v4:$librarySupportVersion"
    supportApi "com.android.support:recyclerview-v7:$librarySupportVersion"
    supportApi "com.android.support:cardview-v7:$librarySupportVersion"

    //Test
    testImplementation 'junit:junit:4.12'

    // AndroidX/Support test components (TODO)
    androidTestAndroidxImplementation 'androidx.test:runner:1.1.0'
    androidTestAndroidxImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestSupportImplementation 'com.android.support.test:runner:1.0.2'
    androidTestSupportImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

sonarqube {
    androidVariant 'supportDebug'
}
