//Added

ext {
    groupId = 'es.rafaco.inappdevtools'     // GroupId
    //artifactId = 'inappdevtools'            // ArtifactId: Renamed into flavor
    //libraryVersion = 'X.X.XX'             // Version: Relocated to root script

    bintrayRepo = "InAppDevTools"
    bintrayPkg = "inappdevtools"

    proyectName = 'InAppDevTools'
    proyectDesc = 'Android library with a collection of tools for debugging, inspecting and reporting from within your own application'

    siteUrl = 'https://github.com/rafaco/InAppDevTools'
    gitUrl = 'https://github.com/rafaco/InAppDevTools.git'
    issuesUrl = 'https://github.com/rafaco/InAppDevTools/issues'
    githubRepo = 'rafaco/InAppDevTools'

    developerId = 'rafaco'
    developerName = 'Rafa Acosta'
    developerEmail = 'rafaco@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
        }
    }
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }
    scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
    }
}

def publicationNames = []
publishing.publications {
    android.libraryVariants.all { variant ->
        if (variant.buildType.name == "debug") return // Prevents publishing debug library

        def flavored = !variant.flavorName.isEmpty()

        /**
         * Translates "_" in flavor names to "-" for artifactIds, because "-" in flavor name is an
         * illegal character, but is well used in artifactId names.
         */
        def variantArtifactId = flavored ? variant.flavorName.replace('_', '-') : project.name

        /**
         * If the javadoc destinationDir wasn't changed per flavor, the libraryVariants would
         * overwrite the javaDoc as all variants would write in the same directory
         * before the last javadoc jar would have been built, which would cause the last javadoc
         * jar to include classes from other flavors that it doesn't include.
         *
         * Yes, tricky.
         *
         * Note that "${buildDir}/docs/javadoc" is the default javadoc destinationDir.
         */
        def javaDocDestDir = file("${buildDir}/docs/javadoc ${flavored ? variantArtifactId : ""}")

        /**
         * Includes
         */
        def sourceDirs = variant.sourceSets.collect {
            it.javaDirectories // Also includes kotlin sources if any.
        }
        def javadoc = task("${variant.name}Javadoc", type: Javadoc) {
            description "Generates Javadoc for ${variant.name}."
            source = variant.javaCompile.source         // javaCompile is deprecated
            destinationDir = javaDocDestDir
            classpath += files(android.getBootClasspath().join(File.pathSeparator))
            classpath += files(configurations.compile)
            options.links("http://docs.oracle.com/javase/7/docs/api/")
            options.links("http://d.android.com/reference/")
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            failOnError false
        }
        def javadocJar = task("${variant.name}JavadocJar", type: Jar, dependsOn: javadoc) {
            description "Puts Javadoc for ${variant.name} in a jar."
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
        def sourcesJar = task("${variant.name}SourcesJar", type: Jar) {
            description "Puts sources for ${variant.name} in a jar."
            from sourceDirs
            classifier = 'sources'
        }

        def publicationName = "${variant.name.capitalize()}"
        //def publicationName = "Iadt${variant.name.capitalize()}Library"
        publicationNames.add(publicationName)

        "$publicationName"(MavenPublication) {
            artifactId variantArtifactId
            group groupId
            version libraryVersionName

            artifact variant.outputs[0].packageLibrary // This is the aar library
            artifact sourcesJar
            artifact javadocJar

            pom {
                packaging 'aar'
                withXml {
                    def root = asNode()
                    root.appendNode("name", proyectName) // "iadt-" + variantArtifactId)
                    root.appendNode("url", siteUrl)
                    root.children().last() + pomConfig

                    def dependenciesNode = root["dependencies"][0] ?: root.appendNode("dependencies")
                    def addDependency = {
                        if (it.group == null) return // Avoid empty dependency nodes
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        if (it.hasProperty('optional') && it.optional) {
                            dependencyNode.appendNode('optional', 'true')
                        }
                    }

                    // Add general dependencies
                    configurations.compile.allDependencies.each addDependency
                    configurations.api.allDependencies.each  addDependency
                    configurations.implementation.allDependencies.each  addDependency

                    // Add flavor dependencies
                    if (flavored) {
                        configurations["${variant.flavorName}Compile"].allDependencies.each addDependency
                        configurations["${variant.flavorName}Api"].allDependencies.each addDependency
                        configurations["${variant.flavorName}Implementation"].allDependencies.each addDependency
                    }
                    //TODO: add builtTypes specific dependencies if needed
                }
            }
        }
    }
}

group = groupId
version = libraryVersionName


afterEvaluate {
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        publications = publicationNames

        dryRun = false      //[Default: false] Skip deployment
        publish = false     //[Default: false] Skip publication after deployment
        override = false    //[Default: false] Override artifacts already published

        pkg {
            desc = proyectDesc
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            issueTrackerUrl = issuesUrl
            licenses = allLicenses
            labels = ['aar', 'android']
            publicDownloadNumbers = true
            //githubRepo = githubRepo
            //githubReleaseNotesFile = 'README.md'

            repo = bintrayRepo
            name = bintrayPkg  //Was project.name
            version {
                desc = proyectDesc
                gpg {
                    sign = true
                    passphrase = System.getenv('BINTRAY_GPG')
                }
            }
        }
    }
}
